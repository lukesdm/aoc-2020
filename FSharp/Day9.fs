// https://adventofcode.com/2020/day/9
module Day9

open Expecto

let parse (input: string): int64 [] =
    input.Replace("\r\n", "\n").Split('\n')
    |> Array.map System.Int64.Parse

let isValid i candidates =
    candidates
    |> Seq.exists (fun (n1, n2) -> i = n1 + n2)

/// Gets non-matching pairs of a sequence. Assumes no identical items in the sequence, as per:
/// "The two numbers will have different values, and there might be more than one such pair"
let pairs inSeq =
    seq {
        for l in inSeq do
            for r in inSeq do
                if l <> r then l, r
    }

/// Finds the invalid number in the data stream i.e. that which can't be generated by summing any pair of the previous windowSize numbers
let findInvalid (windowSize: int) (input: int64 []): int64 =
    let candidates =
        input
        |> Seq.windowed windowSize
        |> Seq.map (fun win -> pairs win)

    let data = input |> Seq.skip windowSize

    let (firstInvalid, _) =
        Seq.map2 (fun d c -> d, isValid d c) data candidates
        |> Seq.find (fun (_, valid) -> not valid)

    firstInvalid

let part1 (input: string) = parse input |> findInvalid 25

// ***PART 2***

/// Finds the range that sums to the invalid number
let findRange (invalid: int64) (input: int64 []) =
    let windowSizes = seq { 2 .. input.Length }

    let windows =
        windowSizes
        |> Seq.collect (fun winSize -> Seq.windowed winSize input)

    let sums =
        windows |> Seq.map (fun win -> win, Seq.sum win)

    sums |> Seq.find (fun (_, sum) -> sum = invalid)

let part2 (input: string) (invalid: int64): int64 =
    let (window, _) = parse input |> findRange invalid
    let min = Seq.min window
    let max = Seq.max window
    min + max

// ***...***

let solve (input: string) =
    let p1 = part1 input

    let p2 = part2 input p1
    (p1, p2)

let example1 = "35
20
15
25
47
40
62
55
65
95
102
117
150
182
127
219
299
277
309
576"

let tests =
    testList
        "Day 9"
        [ test "Can parse" {
            let input = "35
            20
            15
            25"
            let expected = [| 35L; 20L; 15L; 25L |]

            let actual = parse input
            Expect.equal actual expected ""
          }
          test "Example 1 - Can find invalid number" {
              // "In this example, after the 5-number preamble, almost every number is the sum of two of the previous 5 numbers; the only number that does not follow this rule is 127."
              let input = parse example1
              let expected = 127L

              let actual = findInvalid 5 input

              Expect.equal actual expected ""
          }
          test "Part 2 - example 1 - can find weakness" {
              // "To find the encryption weakness, add together the smallest and largest number in this contiguous range; in this example, these are 15 and 47, producing 62."
              let expected = 62L

              let actual = part2 example1 127L

              Expect.equal actual expected ""
          } ]
